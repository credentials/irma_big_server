group 'foundation.privacybydesign.bigregister'
version '1.0'

apply plugin: 'war'
apply plugin: "org.akhikhl.gretty"

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
    cxfTool
}

project.ext {
    cxfVersion = '3.1.11'
    wsdlFile = 'src/wsdl/BIGRegister.wsdl'
    wsdlLocation = '/BIGRegister.wsdl'
    outputDir = file("$buildDir/generated-src/cxf")
    sourceSets.main.java.srcDir outputDir
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.org.akhikhl.gretty:gretty:1.4.2"
    }
}

repositories {
    mavenLocal()
    maven {
        url "https://credentials.github.io/repos/maven2/"
    }
    mavenCentral()
}

dependencies {
    compile 'org.irmacard.api:irma_api_common:0.7.7'
    compile 'foundation.privacybydesign.common:irma_server_common:0.3.2'

    compile 'org.glassfish.jersey.core:jersey-server:2.19'
    compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.19'
    compile 'org.glassfish.jersey.bundles.repackaged:jersey-guava:2.19'
    compile 'org.glassfish.jersey.containers:jersey-container-servlet:2.19'
    compile 'org.slf4j:slf4j-api:1.7.25'

    cxfTool "org.apache.cxf:cxf-tools-wsdlto-frontend-jaxws:$cxfVersion"
    cxfTool "org.apache.cxf:cxf-tools-common:$cxfVersion"
    cxfTool "org.apache.cxf:cxf-tools-wsdlto-core:$cxfVersion"
    cxfTool "org.apache.cxf:cxf-tools-wsdlto-databinding-jaxb:$cxfVersion"
    runtime "org.apache.cxf:cxf-rt-frontend-jaxws:$cxfVersion"
    runtime "org.apache.cxf:cxf-rt-transports-http:$cxfVersion"
    runtime "org.apache.cxf:cxf-rt-transports-http-jetty:$cxfVersion"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task wsdl2java(type: JavaExec) {
    inputs.file file(wsdlFile)
    outputs.dir outputDir
    outputs.upToDateWhen {false}

    // Main Java class to invoke.
    main = 'org.apache.cxf.tools.wsdlto.WSDLToJava'

    // Set classpath to dependencies assigned
    // to the cxf configuration.
    classpath = configurations.cxfTool

    // Arguments to be passed to WSDLToJava.
    args '-d', outputDir
    args '-client'
    args '-verbose'
    args '-validate'
    args '-autoNameResolution'
    args '-wsdlLocation', wsdlLocation
    args wsdlFile
}

compileJava.dependsOn wsdl2java

gretty {
    contextConfigFile = file('src/main/resources/jetty-env.xml')
}
